# Maintainer: Me
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>

_pkgname=zellij
pkgname=zellij-bin
pkgver=0.14.0
pkgrel=1
pkgdesc="A terminal workspace and multiplexer."
arch=('x86_64' 'aarch64')
url="https://zellij.dev"
license=('MIT')
provides=('zellij')
makedepends=('gendesk' 'imagemagick')
conflicts=('zellij-git' 'zellij')
source_x86_64=("https://github.com/zellij-org/${_pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}-x86_64-unknown-linux-musl.tar.gz::https://github.com/zellij-org/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-x86_64-unknown-linux-musl.tar.gz")
source_aarch64=("https://github.com/zellij-org/${_pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}-aarch64-unknown-linux-musl.tar.gz::https://github.com/zellij-org/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-aarch64-unknown-linux-musl.tar.gz")
sha256sums_x86_64=('45ce99d0c13de562f27b46e9693ff0cdf1dba2d15568410fcb67c9b0e9d6b500'
                   'f52cc150cf7aac0fbdf88e6222d7922a73c56d671aea3a0168326f4ec2ed7311')
sha256sums_aarch64=('45ce99d0c13de562f27b46e9693ff0cdf1dba2d15568410fcb67c9b0e9d6b500'
                    '5fa46a00dfffd19b4faf4704847374957dc1e55dd9c7877317372e634f995ece')

prepare() {
    # Generate shell completions
    ./zellij setup --generate-completion bash > zellij.bash
    ./zellij setup --generate-completion fish > zellij.fish
    ./zellij setup --generate-completion zsh > zellij.zsh

    # Resize logo
    convert -resize 128x128 "${_pkgname}-${pkgver}/assets/logo.png" zellij.png

    # Desktop file
    gendesk -f -n \
        --pkgname "${_pkgname}" \
        --pkgdesc "${pkgdesc}" \
        --icon "${_pkgname}" \
        --categories "System;Development" \
        --terminal true \
        --exec "zellij"

}

package() {
    install -Dm755 zellij -t "${pkgdir}/usr/bin"
    install -Dm644 zellij.bash "${pkgdir}/usr/share/bash-completion/completions/${_pkgname}"
    install -Dm644 zellij.fish "${pkgdir}/usr/share/fish/vendor_completions.d/${_pkgname}.fish"
    install -Dm644 zellij.zsh "${pkgdir}/usr/share/zsh/site-functions/_${_pkgname}"
    install -Dm644 zellij.desktop -t "${pkgdir}/usr/share/applications"
    install -Dm644 zellij.png -t "${pkgdir}/usr/share/icons/hicolor/128x128/apps"

    cd "${_pkgname}-${pkgver}"
    install -Dm644 GOVERNANCE.md README.md -t "${pkgdir}/usr/share/doc/${_pkgname}"
    install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${_pkgname}"
}
