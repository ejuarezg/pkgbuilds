# Maintainer: Me
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>

_pkgname=zellij
pkgname=zellij-bin
pkgver=0.12.0
pkgrel=1
pkgdesc="A terminal workspace and multiplexer."
arch=('x86_64' 'aarch64')
url="https://zellij.dev"
license=('MIT')
provides=('zellij')
makedepends=('gendesk' 'imagemagick')
optdepends=('alacritty: start a zellij session using included desktop file')
conflicts=('zellij-git' 'zellij')
source_x86_64=("https://github.com/zellij-org/${_pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}-x86_64-unknown-linux-musl.tar.gz::https://github.com/zellij-org/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-x86_64-unknown-linux-musl.tar.gz"
        "https://raw.githubusercontent.com/zellij-org/zellij/main/assets/logo.png")
source_aarch64=("https://github.com/zellij-org/${_pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}-aarch64-unknown-linux-musl.tar.gz::https://github.com/zellij-org/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-aarch64-unknown-linux-musl.tar.gz"
        "https://raw.githubusercontent.com/zellij-org/zellij/main/assets/logo.png")
sha256sums_x86_64=('bf0be6fedc16d05db04c66cc91fdccb3b278cedf2bfa486d26d4c122436a40ab'
                   '58e1bf184c513fd38e7991a5217fcdbfb53663743f95e03f2c309684affbcb0f'
                   '3e027c6479ac9d6605accc50f7a4b76ac6606d0ca7cde12d1dddd1951e5c64ec')
sha256sums_aarch64=('bf0be6fedc16d05db04c66cc91fdccb3b278cedf2bfa486d26d4c122436a40ab'
                    '5a82fbe63cf786eb4e43f8715c21cb819a0ff3c74d43c8d2ceebbc407dc52073'
                    '3e027c6479ac9d6605accc50f7a4b76ac6606d0ca7cde12d1dddd1951e5c64ec')

prepare() {
    # Generate shell completions
    ./zellij setup --generate-completion bash > zellij.bash
    ./zellij setup --generate-completion fish > zellij.fish
    ./zellij setup --generate-completion zsh > zellij.zsh

    # Resize logo
    convert -resize 128x128 logo.png zellij.png

    # Desktop file
    gendesk -f -n \
        --pkgname "${_pkgname}" \
        --pkgdesc "${pkgdesc}" \
        --icon "${_pkgname}" \
        --categories "System;Development" \
        --exec "alacritty --class zellij,zellij --command zellij"

}

package() {
    install -Dm755 zellij -t "${pkgdir}/usr/bin"
    install -Dm644 zellij.bash "${pkgdir}/usr/share/bash-completion/completions/${_pkgname}"
    install -Dm644 zellij.fish "${pkgdir}/usr/share/fish/vendor_completions.d/${_pkgname}.fish"
    install -Dm644 zellij.zsh "${pkgdir}/usr/share/zsh/site-functions/_${_pkgname}"
    install -Dm644 zellij.desktop -t "${pkgdir}/usr/share/applications"
    install -Dm644 zellij.png -t "${pkgdir}/usr/share/icons/hicolor/128x128/apps"

    cd "${_pkgname}-${pkgver}"
    install -Dm644 GOVERNANCE.md README.md -t "${pkgdir}/usr/share/doc/${_pkgname}"
    install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${_pkgname}"
}
