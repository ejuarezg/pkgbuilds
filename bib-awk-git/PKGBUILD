# Maintainer: Me

_pkgname="bib.awk"
pkgname="bib-awk-git"
pkgver=r16.9b516a0
pkgrel=3
pkgdesc='Bibliography manager written in awk.'
arch=('any')
url="https://github.com/huijunchen9260/$_pkgname"
license=('unknown')
depends=('gawk' 'nawk' 'curl' 'ghostscript' 'xdg-utils')
makedepends=('git' 'make')
optdepends=('xdotool: hide/show terminal when opening graphical software'
            'xclip: clipboard support'
            'xsel: clipboard support'
            'shellect-git: support for non-TUI script; orphaned, not recommended')
provides=('bib-awk')
conflicts=('bib-awk')
source=("$_pkgname::git+${url}.git")
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$_pkgname"

  # Use nawk interpreter instead of mawk
  gawk -i inplace '{gsub(/mawk/, "nawk")};{print}' bib_tui.awk

  # Change path of TEX template
  gawk -i inplace '{gsub(/^\s*TEXTEMP.*/, "    TEXTEMP = \"'"/usr/share/${pkgname%-git}/template.tex"'\"")};{print}' bib.awk
  gawk -i inplace '{gsub(/^\s*TEXTEMP.*/, "    TEXTEMP = \"'"/usr/share/${pkgname%-git}/template.tex"'\"")};{print}' bib_tui.awk
}

package() {
  cd "${_pkgname}"
  PREFIX="$pkgdir/usr" make install
  install -Dm644 template.tex -t "$pkgdir/usr/share/${pkgname%-git}"
  cd "$pkgdir/usr/bin"
  mv bib.awk bib-awk
  mv bib_tui.awk bib-tui-awk
}
